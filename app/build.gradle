apply plugin: 'com.android.application'

static def getAppName() {
    return "abc";
}

static def getCurrentDate() {
    return new Date().format('yyyyMMdd');
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.huicheng.hotel.android" //JPush上注册的包名.
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 13
        versionName "2.0.4.0001"
        multiDexEnabled true
        ndk {
            //选择要添加的对应cpu类型的.so库
            abiFilters 'armeabi', 'armeabi-v7a'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "a04b9ebe26b0d26080195a5b", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }


    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDirs = ['libs']
        }
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        release {
            storeFile file("./huicheng.keystore")
            storePassword "hc123456"
            keyAlias "hc123456"
            keyPassword "hc123456"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled false
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.each { output ->
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    def apkFile = new File(
                            output.outputFile.getParent(),
                            "${getAppName()}_v${variant.versionName}_${getCurrentDate()}_${variant.buildType.name}.apk")
                    output.outputFile = apkFile
                }
            }
    }
}

repositories {
    flatDir {
        dirs 'libs' //this way we can find the .aar file in libs folder
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    debugCompile project(path: ':commonlib', configuration: 'debug')
    releaseCompile project(path: ':commonlib', configuration: 'release')
    //android support library
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    //depend third-party library
    compile 'com.squareup.wire:wire-runtime:2.1.2'
    compile 'com.baoyz.swipemenulistview:library:1.3.0'
    compile 'com.bartoszlipinski.flippablestackview:library:1.2.1'
    compile 'com.zhy:flowlayout-lib:1.0.3'
    compile 'com.android.support:multidex:1.0.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //极光推送JPush
    compile 'cn.jiguang.sdk:jpush:3.0.3'
    compile 'cn.jiguang.sdk:jcore:1.1.1'
    //微信支付
    //    compile 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:1.4.0'
    //内存泄漏检查工具库
    //    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:1.2.1'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0'
    // Glide's OkHttp Integration
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.1@aar'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    //银联全民付
    compile(name: 'qmf-ppplugin-android-2.1.0', ext: 'aar')
    //银联全民花
    compile(name:'cfp-common-T-1013-1553',ext:'aar')
    compile'com.github.DgonLam:SelectAddress:v1.1.7'
    compile'com.github.DgonLam:PhotoActivity:1.0.2'
    compile'com.github.DgonLam:RollViewPager:1.0.0'
    compile'com.github.DgonLam:RefreshLayout:1.0.3'
    //添加阴影工具类
    compile 'com.github.wangjiegulu:ShadowViewHelper:1.0.2'
}